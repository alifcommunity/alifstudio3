#ألف

نص إسم 		= "ألف ستوديو 3"
نص نسخة 	= "1.0.0"

-- المكتبات -----------------------------------
#مكتبة "الواجهة"
#مكتبة "الملفات"
#مكتبة "التشفير"
#مكتبة "الخوارزميات"
#مكتبة "النظام"

-- الكائنات -----------------------------------
كائن نافذة = الواجهة("")
كائن ملفات = الملفات()
كائن تشفير = التشفير()
كائن نظام = النظام()

-- المتغيرات العامة ---------------------------
نص الملف_الحالي_المسار
نص الملف_الحالي_المسار_المجلد
نص الملف_الحالي_المسار_اتطبيق
نص الملف_الحالي_المسار_اسجل
نص الملف_الحالي_النوع
نص الملف_الحالي_الإسم
نص نوع_نظام_التشغيل
منطق هل_ثم_الحفظ = خطأ


-- الدوال - إغلاق ------------------------------
دالة إغلاق
	نافذة:إغلاق_شامل()
نهاية دالة

-- الدوال - فتح ملف ---------------------------
دالة فتح_ملف
	نص المسار = ملفات:فتح_ملف("فتح ملف ألف")
	نص نوع = ملفات:نوع_ملف(المسار)
	إذا نوع = ".alif"
		نص محتوى = ملفات:محتوى_ملف(المسار)
		نص محتوى_64 = تشفير:ترميز_أساس_64(محتوى)
		نافذة:جافاسكريبت("ألف.اضف_كود_64('" + محتوى_64 + "');")
		الملف_الحالي_المسار = المسار
		الملف_الحالي_النوع = نوع
		الملف_الحالي_الإسم = ملفات:إسم_ملف(المسار)
		الملف_الحالي_المسار_المجلد = ملفات:مسار_المجلد(المسار)
		الملف_الحالي_المسار_اتطبيق = الملف_الحالي_المسار_المجلد + الملف_الحالي_الإسم + نظام:نوع_ملف_تطبيق()
		الملف_الحالي_المسار_اسجل = الملف_الحالي_المسار_المجلد + الملف_الحالي_الإسم + ".alif.json"
		هل_ثم_الحفظ = صحيح
		نافذة:جافاسكريبت("alif_set_file_name('" + الملف_الحالي_الإسم + "');")
	وإلا
		المسار = استبدال(المسار، "\\"، "\\\\" )
		نافذة:رسالة("نوع الملف غير مقبول، المرجو إختيار ملف شيفرة ألف (*.alif)" + سطر + "الملف:" + سطر + المسار)
	نهاية إذا
نهاية دالة

-- الدوال - تسجيل ملف -------------------------
دالة تسجيل_ملف
	نص محتوى = نافذة:جافاسكريبت("return ألف.قراءة_كود();")
	محتوى = استبدال(محتوى، "\r"، "" )
	منطق حفظ = ملفات:حفظ_ملف(الملف_الحالي_المسار، محتوى)
	إذا حفظ = صحيح
		هل_ثم_الحفظ = صحيح
	وإلا
		هل_ثم_الحفظ = خطأ
		نافذة:رسالة("عفوا، لم تنجح عملية الحفظ" + سطر + "الملف:" + سطر + الملف_الحالي_المسار)
	نهاية إذا
نهاية دالة

-- الدوال - ترجمة ملف -------------------------
دالة ترجمة (منطق  ترجمة_كاملة)

	نص فحص_سريع
	إذا ترجمة_كاملة = خطأ
		فحص_سريع = " --syntax-only "
	نهاية إذا

	إذا هل_ثم_الحفظ = خطأ
		تسجيل_ملف()
		هل_ثم_الحفظ = صحيح
	نهاية إذا

	إذا ترجمة_كاملة = صحيح
		نافذة:جافاسكريبت("alif_debug('جاري الترجمة..');")
	نهاية إذا

	
	إذا نوع_نظام_التشغيل = "ويندوز"
		نظام:أمر("C:\\Alif3\\alif.exe " + الملف_الحالي_المسار + " --log-json " + الملف_الحالي_المسار_اسجل + " --o " + الملف_الحالي_المسار_اتطبيق + فحص_سريع)
	أو إذا نوع_نظام_التشغيل = "ماك"
		-- ...
	أو إذا نوع_نظام_التشغيل = "لينكس"
		نظام:أمر("alif " + الملف_الحالي_المسار + " --log-json " + الملف_الحالي_المسار_اسجل + " --o " + الملف_الحالي_المسار_اتطبيق + فحص_سريع)
	نهاية إذا

	نص محتوى = ملفات:محتوى_ملف(الملف_الحالي_المسار_اسجل)
	
	إذا محتوى = ""
		نافذة:جافاسكريبت("alif_debug('');")
		إذا ترجمة_كاملة = صحيح
			نظام:أمر(الملف_الحالي_المسار_اتطبيق)
		نهاية إذا
	وإلا
		محتوى = استبدال(محتوى، "'"، "\\'" )
		نافذة:جافاسكريبت("alif_debug_json('" + محتوى + "');")
	نهاية إذا

نهاية دالة

دالة ترجمة_ملف
	ترجمة (صحيح)
نهاية دالة

دالة فحص_الشيفرة
	ترجمة (خطأ)
نهاية دالة

-- الدوال - تغيير المحتوى ---------------------
دالة ثم_تغيير
	هل_ثم_الحفظ = خطأ
نهاية دالة

-- الرئيسية -----------------------------------
دالة رئيسية

  -- إستعداد
  نوع_نظام_التشغيل = نظام:نوع()

  -- ربط الأزرار مع الدوال
  نافذة:ربط("خروج"، إغلاق)
  نافذة:ربط("فتح"، فتح_ملف)
  نافذة:ربط("حفظ"، تسجيل_ملف)
  نافذة:ربط("ترجمة"، ترجمة_ملف)
  نافذة:ربط("ثم_تغيير"، ثم_تغيير)
  نافذة:ربط("فحص_الشيفرة"، فحص_الشيفرة)

  -- إنشاء خادم جديد
  نص المسار = نافذة:المسار_الحالي()
  نص عنوان_الخادم = نافذة:إخدم_مجلد(المسار)
  نص المسار_الكامل = المسار + "index.html?webui=" + عنوان_الخادم + "/webuicore.js"
  
  'اطبع(سطر + "Alif Studio 3 (v" + نسخة + ")" + سطر + سطر)
  'اطبع("CWD: \t\t\t" + المسار + سطر)
  'اطبع("Server Address: \t" + عنوان_الخادم + سطر)
  'اطبع("Complete URL: \t\t" + المسار_الكامل + سطر)
  'اطبع(سطر + "Starting.." + سطر)

  -- إظهار النافذة
  نافذة:إظهار_متصفح_مع_مسار (نافذة:كروم، المسار_الكامل)
  نافذة:إنتظار()

نهاية دالة
